buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven-publish'

version = "0.6.4"
group= "gigaherz.commons" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "${project.group}-1.12.1"

minecraft {
    version = "1.12.1-14.22.0.2444"
    runDir = "run"

    mappings = "snapshot_20170804"
}

jar {
    manifest {
        attributes("Maven-Artifact":"${project.group}:${project.archivesBaseName}:${project.version}")
    }
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.encoding = 'UTF-8'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

tasks.publish.dependsOn build
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId project.archivesBaseName
            from components.java

            artifact sourceJar
        }
    }

    repositories {
        maven {
            url "$projectDir/../CommonMaven"
        }
    }
}


